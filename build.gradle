plugins {
    id 'com.github.spotbugs' version '5.0.9' apply false
}

allprojects {
    apply plugin: 'java'

    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    group 'com.github.dharbuzov'
    version '0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    checkstyle {
        toolVersion '9.1'
        configFile file("../config/checkstyle/checkstyle.xml")
    }

    spotbugs {
        toolVersion = '4.7.2'
        ignoreFailures = true
        showProgress = true
        reportsDir = file("$buildDir/spotbugs")
    }

    spotbugsMain {
        reports {
            html {
                required = true
                reportLevel = 'high'
                outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }

    spotbugsTest {
        reports {
            html {
                required = true
                outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        lombokVersion = '1.18.24'
        jupiterVersion = '5.9.0'
        jupiterParamsVersion = '5.9.0'
        mockitoVersion = '4.8.0'
        nettyVersion = '4.1.82.Final'
        awaitilityVersion = '4.2.0'
        slf4jVersion = '2.0.3'
        spotbugsAnnotationsVersion = '4.7.2'
    }

    dependencies {
        compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsAnnotationsVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"

        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.junit.jupiter:junit-jupiter-params:$jupiterParamsVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testImplementation "org.awaitility:awaitility:$awaitilityVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    test {
        useJUnitPlatform()
    }
}
